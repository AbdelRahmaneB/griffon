/*
 * Copyright 2010-2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'application'

mainClassName = 'editor.Launcher'
ext.griffonVersion = '2.0.0-SNAPSHOT'

configurations {
    compileOnly
}

repositories {
    jcenter()
    maven {
        name 'Kordamp'
        url 'http://dl.bintray.com/content/aalmiray/kordamp'
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    compileOnly "org.codehaus.griffon:griffon-groovy-compile:$griffonVersion"

    compile "org.codehaus.griffon:griffon-groovy:$griffonVersion"
    compile "org.codehaus.griffon:griffon-swing:$griffonVersion"

    runtime "org.codehaus.griffon:griffon-guice:$griffonVersion"
    runtime "org.codehaus.griffon:griffon-swing-builder:$griffonVersion"

    runtime('log4j:log4j:1.2.17') {
        exclude group: 'ant',         module: 'ant-nodeps'
        exclude group: 'ant',         module: 'ant-junit'
        exclude group: 'ant-contrib', module: 'ant-contrib'
    }
    runtime 'org.slf4j:slf4j-log4j12:1.7.5'
}

sourceSets {
    main {
        compileClasspath += configurations.compileOnly
        groovy {
            srcDirs = [
                'griffon-app/conf',
                'griffon-app/controllers',
                'griffon-app/models',
                'griffon-app/views',
                'griffon-app/services',
                'griffon-app/lifecycle',
                'src/main/groovy'
            ]
        }
        resources {
            srcDirs = [
                'griffon-app/resources',
                'griffon-app/i18n',
                'src/main/resources'
            ]
        }
    }
}

javadoc {
    classpath += configurations.compileOnly
}

idea {
    module {
        scopes.PROVIDED.plus += configurations.compileOnly
    }
}

gradle.taskGraph.whenReady {
    sourceSets.main.groovy.srcDirs.each { it.mkdirs() }
    sourceSets.main.resources.srcDirs.each { it.mkdirs() }
}