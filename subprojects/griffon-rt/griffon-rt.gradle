/*
 * Copyright 2010-2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'groovy'
apply plugin: 'osgi'

ext.pomDescription = 'Griffon runtime'

dependencies {
    groovy "org.codehaus.groovy:groovy-all:$groovyVersion"

    compile("org.codehaus.groovy:groovy-all:$groovyVersion")
    compile("log4j:log4j:$log4jVersion") {
        exclude group: 'ant',         module: 'ant-nodeps'
        exclude group: 'ant',         module: 'ant-junit'
        exclude group: 'ant-contrib', module: 'ant-contrib'
    }
    compile("org.slf4j:slf4j-log4j12:$slf4jVersion") {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'log4j',     module: 'log4j'
    }
    compile("org.slf4j:slf4j-api:$slf4jVersion")
    compile("org.slf4j:jcl-over-slf4j:$slf4jVersion")  {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compile("org.slf4j:jul-to-slf4j:$slf4jVersion")  {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    testCompile 'junit:junit:4.10'
}

task osgiBundleSingle(type: Jar, dependsOn: jar) {
    archiveName    = "org.codehaus.griffon.rt-${project.version}.SINGLE.jar"
    from sourceSets.main.output

    manifest = osgiManifest {
        classesDir = sourceSets.main.output.classesDir
        classpath  = configurations.runtime

        instruction 'Bundle-Vendor', 'griffon-framework.org'
        instruction 'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.6'
    }
}

task copyDependencies(type: Copy, dependsOn: jar) {
    destinationDir = "${buildDir}/bundle" as File
    from(configurations.compile) {
        exclude 'groovy*'
    }
}

task osgiBundleFull(type: Jar, dependsOn: copyDependencies) {
    archiveName    = "org.codehaus.griffon.rt-${project.version}.FULL.jar"
    from sourceSets.main.output
    from copyDependencies.destinationDir

    manifest = osgiManifest {
        classesDir = sourceSets.main.output.classesDir
        classpath  = configurations.runtime

        instruction 'Bundle-ActivationPolicy', 'lazy'
        instruction 'Bundle-Vendor', 'griffon-framework.org'
        instruction 'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.6'
        instruction 'Export-Package', "org.apache.log4j;version=\"${log4jVersion}\","+
                                      "org.apache.log4j.*;version=\"${log4jVersion}\","+
                                      "org.slf4j;version=\"${slf4jVersion}\"," +
                                      "*;-noimport:=true;version=${version}"
        instruction 'Export-Package', "org.apache.log4j.helpers;version=\"${log4jVersion}\""
        instruction 'Export-Package', "org.apache.log4j.xml;version=\"${log4jVersion}\""
        instruction 'Export-Package', "org.apache.log4j.varia;version=\"${log4jVersion}\""
        instruction 'Export-Package', "org.apache.log4j.spi;version=\"${log4jVersion}\""
        instruction 'Export-Package', "org.apache.log4j;version=\"${log4jVersion}\""
        instruction 'Export-Package', "org.slf4j;version=\"${slf4jVersion}\""
        instruction 'Import-Package', '!org.apache.log4j.*,!org.slf4j,config,addon,*'
        instruction 'Bundle-ClassPath', ".,${copyDependencies.destinationDir.listFiles().collect([]){ it.name }.join(',')}"
    }
}

task osgiBundle(dependsOn: [osgiBundleSingle, osgiBundleFull]) {}