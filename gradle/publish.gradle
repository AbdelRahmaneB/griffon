/*
 * Copyright 2010-2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom.withXml {
                asNode().with {
                    appendNode('name', project.name)
                    appendNode('description', project.ext.pomDescription)
                    appendNode('url', 'http://griffon-framework.org')
                    appendNode('inceptionYear', '2008')
                    appendNode('licenses')
                        .appendNode('license')
                        .appendNode('name', 'The Apache Software License, Version 2.0').parent()
                        .appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt').parent()
                        .appendNode('distribution', 'repo')
                    appendNode('scm')
                        .appendNode('url', 'https://github.com/griffon/griffon')
                    def developers = appendNode('developers')
                    [
                        aalmiray: 'Andres Almiray',
                        shemnon: 'Danno Ferrin',
                        jwill: 'James Williams',
                        aklein: 'Alexander Klein',
                        rgroeschke: 'Rene Groeschke'
                    ].each { devId, devName ->
                        developers.appendNode('developer')
                            .appendNode('id', devId).parent()
                            .appendNode('name', devName).parent()
                            .appendNode('roles')
                            .appendNode('role', 'Developer')
                    }
                    def contributors = appendNode('contributors')
                    [
                        'Dierk Koenig',
                        'Guillaume Laforge',
                        'Dean Iverson'
                    ].each { cName ->
                        contributors.appendNode('contributor')
                            .appendNode('name', cName).parent()
                            .appendNode('roles')
                            .appendNode('role', 'Contributor')
                    }
                }
            }

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}
