/*
 * Copyright 2010-2011 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven'

configurations {
    deployerJars
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-webdav:1.0-beta-2',
                 'org.apache.maven:maven-ant-tasks:2.1.0'
}

artifactData = new ConfigSlurper().parse(file("${rootProject.rootDir}/src/artifacts.groovy").toURI().toURL())

installer = install.repositories.mavenInstaller
def deployer = null

// GRIFFON-291 these properties should be optional
if(!project.hasProperty('codehausUsername')) codehausUsername = ''
if(!project.hasProperty('codehausPassword')) codehausPassword = ''

uploadArchives {
    repositories {
        deployer = mavenDeployer {
            name = 'codehausDeployer'
            configuration = configurations.deployerJars
            uploadDescriptor = true
            repository(url: codehausReleaseUrl) {
                authentication(userName: codehausUsername, password: codehausPassword)
            }
            snapshotRepository(url: codehausSnapshotUrl) {
                uniqueVersion = false
                authentication(userName: codehausUsername, password: codehausPassword)
            }
        }
    }
}

[installer, deployer].each { repo ->
    configure(repo) {
        pom.project {
            def dataName = project.name - "griffon-"
            name(artifactData[dataName].name)
            description(artifactData[dataName].description)
            url('http://griffon.codehaus.org')
            inceptionYear '2008'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url "https://github.com/griffon/griffon"
            }
            developers {
                [aalmiray: 'Andres Almiray',
                 shemnon: 'Danno Ferrin',
                 jwill: 'James Williams',
                 aklein: 'Alexander Klein',
                 rgroeschke: 'Rene Groeschke'].each { devId, devName ->
                    developer {
                        id devId
                        name devName
                        roles {
                            role 'Developer'
                        }
                    }
                }
            }
            contributors {
                ['Dierk Koenig',
                 'Guillaume Laforge',
                 'Dean Iverson'].each { devName ->
                    contributor {
                        name devName
                        roles {
                            role 'Contributor'
                        }
                    }
                }
            }
        }
     }
}
