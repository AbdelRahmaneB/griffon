/*
 * Copyright 2010-2011 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

task assembleWrapper(dependsOn: [wrapperClasses]) {
    inputs.dir sourceSets.wrapper.classes
    outputs.dir new File("$buildDir/assemble/wrapper")

    description = "Merges compiled Wrapper classes with original classes"

    doLast {
        ant.unjar(dest: "$buildDir/assemble/wrapper", src: "$projectDir/lib/wrapper/gradle-wrapper.jar") {
            patternset {
                exclude(name: "META-INF/**")
            }
        }

        copy {
            from sourceSets.wrapper.classes
            into "$buildDir/assemble/wrapper"
            include '**/*.class'
        }
    }
}

task jarWrapper(type: Jar, dependsOn: ':assembleWrapper') {
    description = "Jars all merged Wrapper classes"
    archiveName = 'griffon-wrapper.jar'
    destinationDir = "$buildDir/assemble/jars" as File
    dependsOn sourceSets.wrapper.classes
    from "$buildDir/assemble/wrapper"
    include('org/gradle/wrapper/**')
}

task distWrapper(type: Copy, dependsOn: ':jarWrapper') {
    description = "Creates a wrapper distribution"
    destinationDir = "$buildDir/distributions/wrapper" as File
    from("$buildDir/assemble/jars") {
        include 'griffon-wrapper.jar'
        into 'wrapper'
    }
    from('src/wrapper') {
        include '*.properties'
        into 'wrapper'
        filter(ReplaceTokens, tokens: ['griffon.version': version])
    }
    from('src/wrapper/bin') {
        filter(ReplaceTokens, tokens: ['griffon.version': version])
    }

    doLast {
        ant.chmod(dir: "$buildDir/distributions/wrapper", includes: 'griffonw', perm: 'ugo+x')
    }
}
