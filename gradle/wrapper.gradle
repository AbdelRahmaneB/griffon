import org.apache.tools.ant.filters.ReplaceTokens

task assembleWrapper(type: Copy) {
    description = "Merges compiled Wrapper classes with original classes"
    ant.unjar(dest: "$buildDir/assemble/wrapper", src: "$projectDir/lib/wrapper/gradle-wrapper.jar") {
        patternset {
            exclude(name: "META-INF/**")
        }
    }

    from sourceSets.wrapper.classes
    into "$buildDir/assemble/wrapper"
    include '**/*.class'
}

task jarWrapper(type: Jar, dependsOn: ':assembleWrapper') {
    description = "Jars all merged Wrapper classes"
    archiveName = 'griffon-wrapper.jar'
    destinationDir = "$buildDir/assemble/jars" as File
    dependsOn sourceSets.wrapper.classes
    from "$buildDir/assemble/wrapper"
    include('org/gradle/wrapper/**')
}

task distWrapper(type: Copy, dependsOn: ':jarWrapper') {
    description = "Creates a wrapper distribution"
    destinationDir = "$buildDir/distributions/wrapper" as File
    from("$buildDir/assemble/jars") {
        include 'griffon-wrapper.jar'
        into 'wrapper'
    }
    from('src/wrapper') {
        include '*.properties'
        into 'wrapper'
        filter(ReplaceTokens, tokens: ['griffon.version': version])
    }
    from('src/wrapper/bin') {
        filter(ReplaceTokens, tokens: ['griffon.version': version])
    }

    doLast {
        ant.chmod(dir: "$buildDir/distributions/wrapper", includes: 'griffonw', perm: 'ugo+x')
    }
}
