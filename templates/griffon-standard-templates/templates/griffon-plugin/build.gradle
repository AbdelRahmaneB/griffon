import java.text.SimpleDateFormat

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.codehaus.griffon:gradle-griffon-plugin:1.0.0.BETA2'
        classpath 'org.codehaus.griffon:gradle-griffon-build-plugin:1.0.0.BETA2'
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.4'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.4.1'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.9.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.4.1'
        classpath 'org.ajoberstar:gradle-git:0.9.0'
    }
}

apply plugin: 'base'
apply plugin: 'idea'
apply plugin: 'coveralls'
apply plugin: 'build-dashboard'
apply from: 'gradle/idea.gradle'
apply plugin: 'org.codehaus.griffon.griffon-build'
apply from: 'gradle/bom.gradle'

Date buildTimeAndDate = new Date()
ext {
    buildTime = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildDate = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'idea'

    repositories {
        jcenter()
    }

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

subprojects { subproj ->
    plugins.withType(JavaPlugin) {
        configurations {
            compileOnly
            testCompileOnly
        }

        sourceSets {
            main {
                compileClasspath += [configurations.compileOnly]
            }
            test {
                compileClasspath += [configurations.testCompileOnly]
            }
        }

        javadoc {
            classpath += [configurations.compileOnly]
        }

        idea {
            module {
                scopes.PROVIDED.plus += [configurations.compileOnly]
                scopes.PROVIDED.plus += [configurations.testCompileOnly]
            }
        }

        dependencies {
            testCompile 'junit:junit:4.11'
            testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
            testCompile 'org.slf4j:slf4j-simple:1.7.7'
        }

        task sourceJar(type: Jar) {
            group 'Build'
            description 'An archive of the source code'
            classifier 'sources'
            from sourceSets.main.allSource
        }

        tasks.withType(AbstractCompile) {
            if (rootProject.hasProperty('lint') && rootProject.lint.toBoolean()) {
                options.compilerArgs = [
                    '-Xlint:all', '-Xlint:deprecation', '-Xlint:unchecked'
                ]
            }
        }

        subproj.apply from: rootProject.file('gradle/publishing.gradle')
        subproj.apply from: rootProject.file('gradle/code-quality.gradle')

        if (subproj.publishJars.toBoolean()) {
            javadoc {
                excludes = ['**/*.html', 'META-INF/**', '**/*.groovy']

                options.use         = true
                options.splitIndex  = true
                options.encoding    = 'UTF-8'
                options.author      = true
                options.version     = subproj.sourceCompatibility
                options.windowTitle = "\$subproj.projectDescription \$version"
                options.docTitle    = "\$subproj.projectDescription \$version"
                options.footer      = rootProject.javadocFooter
                options.links       = ['http://www.slf4j.org/apidocs/',
                                       'http://jsr-305.googlecode.com/svn/trunk/javadoc/',
                                       'http://docs.oracle.com/javase/7/docs/api/',
                                       'http://griffon.github.io/griffon/guide/api/']

                doLast { task ->
                    copy {
                        into task.destinationDir
                        from project(':griffon-${plugin_name}-guide').file('src/javadoc/resources/img/griffon.ico'),
                             project(':griffon-${plugin_name}-guide').file('src/javadoc/resources/css/stylesheet.css')
                    }
                    copy {
                        into file("\${task.destinationDir}/resources")
                        from project(':griffon-${plugin_name}-guide').file('src/javadoc/resources/img/')
                    }
                }
            }
        }
    }
}