/*
 * Copyright 2008-2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'lazybones-templates'

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

lazybones {
    repositoryUrl = 'https://api.bintray.com/content/griffon/griffon-lazybones-templates'
    repositoryUsername = project.bintrayUsername
    repositoryApiKey = project.bintrayApiKey
}

// A bit hacky but gets the job done -> must replace
// @griffon.version@ on lazybones.groovy
tasks.withType(Zip) { task ->
    task.doFirst {
        copy {
            File destDir = task.source.files.iterator().next().parentFile
            into destDir
            if (file("sources/${destDir.name}").exists()) {
                from(file("sources/${destDir.name}")) {
                    filter(org.apache.tools.ant.filters.ReplaceTokens,
                        tokens: ['griffon.version': project.version])
                }
            }

            if (destDir.name.startsWith('subtmpl-')) {
                // continue
            } else if (destDir.name == 'griffon-plugin') {
                from(rootProject.file('templates/shared-files/sources')) {
                    exclude 'installer'
                    exclude 'maven'
                    exclude 'gradle/package.gradle'
                    filter(org.apache.tools.ant.filters.ReplaceTokens,
                        tokens: ['griffon.version': project.version])
                }
            } else {
                from(rootProject.file('templates/shared-files/sources')) {
                    filter(org.apache.tools.ant.filters.ReplaceTokens,
                        tokens: ['griffon.version': project.version])
                }
                from(rootProject.file('src/media')) {
                    include 'griffon*'
                    into 'src/media'
                }
            }
        }
    }
}