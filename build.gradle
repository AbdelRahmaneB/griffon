/*
 * Copyright 2010-2011 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

apply plugin: 'base'
apply plugin: 'idea'

configurations {
	runtime { transitive = false }
	docs
	groovy
}

repositories {
	mavenCentral()
    mavenRepo name: 'Codehaus', url: 'http://repository.codehaus.org/'
    mavenRepo name: 'Grails Central', url: 'http://repo.grails.org/grails/core/'
	mavenRepo name: 'SpringSource', url: 'http://repository.springsource.com/maven/bundles/release'
    mavenRepo name: 'JavaNet', url: 'http://download.java.net/maven/2/'
}

evaluationDependsOn(':griffon-rt')
evaluationDependsOn(':griffon-cli')
evaluationDependsOn(':griffon-resources')
evaluationDependsOn(':griffon-scripts')
evaluationDependsOn(':griffon-wrapper')

dependsOnChildren()

dependencies {
	groovy localGroovy() //for api docs generation needed (groovydoc)
    
	runtime project(":griffon-rt")
	runtime project(":griffon-cli")
	runtime project(":griffon-resources")
	runtime project(":griffon-scripts")

	// problems with ant classloader makes it necessary to duplicate the deps
	// in buildSrc/ and the docs configurations
    docs group: 'org.grails', name: 'grails-docs', version: '1.3.6'
    docs group: 'radeox', name: 'radeox', version: '1.0-b2'
    docs group: 'com.lowagie', name: 'itext', version: '2.0.8'
    docs group: 'org.slf4j', name: 'slf4j-api', version: '1.6.3'
    docs group: 'org.xhtmlrenderer', name: 'core-renderer', version: 'R8'
    docs group: 'commons-logging', name: 'commons-logging', version: '1.1.1'
}

subprojects { subproj ->
	apply plugin: 'java'
	apply plugin: 'idea'
	apply from: "${rootDir}/gradle/maven.gradle"
	apply from: "${rootDir}/gradle/coverage.gradle"
	
    sourceCompatibility = '1.5'
	targetCompatibility = '1.5'
	group = 'org.codehaus.griffon'

	repositories {
	    mavenLocal()
	    mavenCentral()
	    mavenRepo name: 'Codehaus', url: 'http://repository.codehaus.org'
        mavenRepo name: 'Grails Central', url: 'http://repo.grails.org/grails/core/'
	    mavenRepo name: 'SpringSource', url: 'http://repository.springsource.com/maven/bundles/release'
		mavenRepo name: 'Gradle', url: 'http://gradle.artifactoryonline.com/gradle/libs-releases-local'
	}

    jar.manifest {
        Date buildTime = new Date()
	    attributes(
			'Built-By': System.properties['user.name'],
			'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
			'Build-Date': new SimpleDateFormat('dd-MMM-yyyy').format(buildTime),
			'Build-Time': new SimpleDateFormat('hh:mm aa').format(buildTime),
			'Specification-Title': subproj.name,
			'Specification-Version': project.version,
			'Specification-Vendor': 'griffon.codehaus.org',
			'Implementation-Title': subproj.name,
			'Implementation-Version': project.version,
			'Implementation-Vendor': 'griffon.codehaus.org'
	    )
	}
}

// @todo move apply statements to the top of the build script
apply from: "${rootDir}/gradle/docs.gradle"
apply from: "${rootDir}/gradle/package.gradle"
apply from: "${rootDir}/gradle/installer.gradle"

task allTests {
    dependsOn { [project(':griffon-rt').test] }
    description = "Runs all tests."
}

task fullDist(dependsOn: [zipGuide, zipBinary, zipSource, tarBinary, tarSource, izpack, deb]) {
    description = "Assembles all packages."
}

task bambooStage1(dependsOn: [clean, allTests]) {
    description = "Makes a full test run and assembles all packages."
}

task bambooStage2(dependsOn: [bambooStage1, fullDist, rpm]) {
    description = "Makes a full test run and assembles all packages."
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-5'
    jarFile = 'gradle/wrapper/gradle-wrapper.jar'
}
